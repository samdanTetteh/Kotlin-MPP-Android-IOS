apply plugin: 'com.android.library'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.squareup.sqldelight'

kotlin {
    targets {
        final def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'iOS') {
            binaries {
                framework('SharedCode')
            }
        }

        fromPreset(presets.android, 'android')
    }

    sourceSets {
        all {
            languageSettings.progressiveMode = true
        }

        commonMain.dependencies {
            api 'org.jetbrains.kotlin:kotlin-stdlib-common'
            api "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:1.3.2"
            api "io.ktor:ktor-client-core:1.3.2"
            api "io.ktor:ktor-client-json:1.3.2"
            api "io.ktor:ktor-client-logging:1.3.2"
            api "io.ktor:ktor-client-serialization:1.3.2"
            api "com.squareup.sqldelight:runtime:1.3.0"
            api "androidx.arch.core:core-testing:2.1.0"
        }

        commonTest.dependencies {
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.5"
            implementation "org.jetbrains.kotlin:kotlin-test-common:1.3.72"
            implementation "org.jetbrains.kotlin:kotlin-test-annotations-common:1.3.72"
            api "io.ktor:ktor-client-mock:1.3.2"
        }


        androidMain.dependencies {
            api 'org.jetbrains.kotlin:kotlin-stdlib'
            api "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2"
            api "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5"
            api "io.ktor:ktor-client-android:1.3.2"
            api "io.ktor:ktor-client-core-jvm:1.3.2"
            api "io.ktor:ktor-client-json-jvm:1.3.2"
            api "io.ktor:ktor-client-logging-jvm:1.3.2"
            api "org.slf4j:slf4j-simple:1.7.30"
            api "io.ktor:ktor-client-mock-jvm:1.3.2"
            api "io.ktor:ktor-client-serialization-jvm:1.3.2"
            api "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"
            api "com.squareup.sqldelight:android-driver:1.3.0"
        }

        iOSMain {
            dependsOn commonMain
            dependencies {
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.3.2"
                api "io.ktor:ktor-client-ios:1.3.2"
                api "io.ktor:ktor-client-core-native:1.3.2"
                api "io.ktor:ktor-client-json-native:1.3.2"
                api "io.ktor:ktor-client-logging-native:1.3.2"
                api "io.ktor:ktor-client-serialization-native:1.3.2"
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:0.20.0"
                api "com.squareup.sqldelight:native-driver:1.3.0"
                api "io.ktor:ktor-client-mock-native:1.3.2"
            }
        }


        androidTest{
            dependencies {
                api "androidx.arch.core:core-testing:2.0.0"
                api "androidx.test.ext:junit:1.1.1"
                api "io.ktor:ktor-client-mock:1.3.2"
            }
        }


        iOSTest{
            dependencies {
                api "io.ktor:ktor-client-mock-native:1.3.2"
            }
        }
    }
}


sqldelight {
  Database{
      packageName = "com.superawesome.multiplatform.db"
  }
}


// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

/**
 * Setup Android specific info for androidMain sub-library
 */

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
            java.srcDirs = ['src/androidMain/kotlin']
            res.srcDirs = ['src/androidMain/res']
        }
    }

    testOptions.unitTests.includeAndroidResources = true
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

/**
 * Pack this Kotlin code into an iOS framework.
 * You can find the result in:
 *  sharedcode/build/xcode-frameworks
 */
task packForXCode(type: Sync) {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    final String mode = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'
    final def framework = kotlin.targets.iOS.binaries.getFramework("SharedCode", mode)

    inputs.property "mode", mode
    dependsOn framework.linkTask

    from { framework.outputFile.parentFile }
    into frameworkDir

    doLast {
        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
    }
}
tasks.build.dependsOn packForXCode